# Arch Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Kupfer: InsanePrawn
_mode=host
pkgname=modemmanager-qcom
#pkgname=(modemmanager-qcom libmm-qcom-glib)
pkgdesc="Mobile broadband modem management service"
pkgver=1.18.8
pkgrel=2
_arches=specific
arch=(aarch64)
license=(GPL2)
url=https://www.freedesktop.org/wiki/Software/ModemManager/
depends=(
    systemd
    libgudev
    polkit
    ppp
    libqmi
    libmbim
    mobile-broadband-provider-info
)
optdepends=(
    "usb_modeswitch: install if your modem shows up as a storage drive"
)
makedepends=(
    gtk-doc
    gobject-introspection
    vala
    meson
    git
    bash-completion
)
checkdepends=(
    python-gobject
    python-dbus
)
provides=(
    modemmanager
    libmm-glib
    libmm-qcom-glib
    libmm-glib.so
)
replaces=(
    modemmanager
    libmm-glib
)
conflicts=(
    modemmanager
    libmm-glib
)
options=(
    debug
)
_commit=0d8b5e93fc62eb0f41e18a2d9d845331d7af36ec  # tags/1.18.8^0
source=("git+https://gitlab.freedesktop.org/mobile-broadband/ModemManager.git#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
  cd ModemManager
  git describe --tags | sed 's/-rc/rc/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd ModemManager
}

build() {
  local meson_options=(
    -D dbus_policy_dir=/usr/share/dbus-1/system.d
    -D dist_version="\"$pkgver-$pkgrel\""
    -D gtk_doc=false
    -D plugin_qcom_soc=enabled
    -D polkit=permissive
    -D vapi=true
  )

  arch-meson ModemManager build "${meson_options[@]}"
  meson compile -C build
}

check() {
  # Tests don't work yet
  # meson test -C build --print-errorlogs
  return
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}


# FIXME: fix .so dependency handling in kupferbootstrap and reintroduce split pkg
#package_modemmanager-qcom() {
package() {
  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"
  _pick libmm usr/include
  _pick libmm usr/lib/girepository-1.0
  _pick libmm usr/lib/libmm-glib.so*
  _pick libmm usr/lib/pkgconfig
  _pick libmm usr/share/gir-1.0
  _pick libmm usr/share/vala

  # former libmm-glib
  cd "$srcdir/libmm"
  find -type f -print0 | sed -z 's_^./__g' | xargs -0 -n1 -t -i install -Dm644 {} "$pkgdir"/{}
}

