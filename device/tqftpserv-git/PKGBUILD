_mode=host
_pkgname=tqftpserv
pkgname="$_pkgname-git"
pkgdesc="Trivial File Transfer Protocol server over AF_QIPCRTR"
pkgver=r12.783425b
pkgrel=2
_arches=specific
arch=(aarch64)
license=(BSD-3-Clause)
url="https://github.com/andersson/$_pkgname"
provides=("$_pkgname")
depends=(qrtr-git)
_commit=783425b550de2a359db6aa3b41577c3fbaae5903
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd "$_pkgname"

    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
    cd "$_pkgname"

    make prefix=/usr
}

package() {
    cd "$_pkgname"

    make prefix=/usr DESTDIR="$pkgdir/" install
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
