_mode=cross
_nodeps=true
pkgbase=device-sdm845-xiaomi-beryllium
pkgname=(
    device-sdm845-xiaomi-beryllium-common
    device-sdm845-xiaomi-beryllium-ebbg
    device-sdm845-xiaomi-beryllium-tianma
)
pkgver=0.4
pkgrel=1
_arches=specific
arch=(aarch64)
license=(MIT)
makedepends=(devicepkg-helpers)
_commit=1770b37b601337b256258fcfbc18e53f895c9d81
source=(
    "https://gitlab.com/postmarketOS/pmaports/-/raw/${_commit}/device/community/device-xiaomi-beryllium/deviceinfo"
    xiaomi-beryllium.conf
    machine-info
    rootston.ini
)
sha256sums=(
    5c22068d2e45fcb72dcd42c795acb98cae9c329f86b4d7567c7b08e3a67135cb
    6591192b2696f0b6594e5c8724075752158eab2ea537d77c4d9bc3ead61f305e
    7729d6a89258b470cadd48cfbc704bab7791501c53f45f50737dea6b441a3171
    3ecab866e26f244bd0e449f6b56b109f362aed3fa6c95738428b222cc5766cd6
)

package_device-sdm845-xiaomi-beryllium-common() {
    depends=(
        device-sdm845-common
        firmware-sdm845-xiaomi-beryllium
    )
    install -Dm644 "$srcdir"/xiaomi-beryllium.conf "$pkgdir"/etc/kupfer/mkinitcpio.conf.d/xiaomi-beryllium.conf
    install -Dm644 "$srcdir"/machine-info "$pkgdir"/etc/machine-info
    install -Dm644 "$srcdir"/rootston.ini "$pkgdir"/etc/phosh/rootston.ini
}

package_device() {
    local variant="$1"
    cp "$srcdir"/deviceinfo "$srcdir"/deviceinfo_
    filter_deviceinfo "$variant" "$srcdir"/deviceinfo_
    cat >>"$srcdir"/deviceinfo_ <<EOF

deviceinfo_partitions_data="/dev/sda21"
deviceinfo_partitions_microsd="/dev/mmcblk0"
EOF
    install -Dm644 "$srcdir"/deviceinfo_ "$pkgdir"/etc/kupfer/deviceinfo
}

package_device-sdm845-xiaomi-beryllium-ebbg() {
    depends=(device-sdm845-xiaomi-beryllium-common)
    package_device ebbg
}

package_device-sdm845-xiaomi-beryllium-tianma() {
    depends=(device-sdm845-xiaomi-beryllium-common)
    package_device tianma
}
