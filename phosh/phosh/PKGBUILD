_mode=host
pkgname=phosh
pkgdesc="The phosh Shell"
pkgver=0.26.0.r0.g30a36e8
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL)
url=https://gitlab.gnome.org/World/Phosh/phosh
depends=(
    gtk3
    libhandy
    libadwaita
    gnome-desktop
    gnome-session
    gnome-shell
    upower
    libpulse
    gcr
    feedbackd
    libnm
    phoc
    callaudiod
    evolution-data-server
)
optdepends=(
    "firefox-wayland-config: Use Wayland by default for Firefox"
    "qt-wayland-config: Use Wayland by default for Qt apps"
    "firefox-mobile-config: Firefox configuration optimized for mobile"
    "qt-mobile-config: Qt conf.d entry to disable desktop window decorations"
)
makedepends=(
    meson
    ninja
    evince
)
_commit=30a36e88ab762d4729c7fb0d7a9fc8cbd447656d
source=(
    "git+$url#commit=$_commit"
    pam_phosh
    phosh.service
)
sha256sums=(
    SKIP
    43b94d0d9f4d083f028c77d18cb0d0f8037d160c41f333878c7cae3df0163c3d
    8bdf19952cdec76cc3d45d3be03aed3fd1c1815c0560317404c50293e17a1ded
)

pkgver() {
    cd "$pkgname"
    git describe --long --tags --abbrev=7 "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson ${pkgname} output --libexecdir "lib/$pkgname" -Dtests=false -Dphoc_tests=disabled -Dsystemd=true
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
}
