_mode=host
pkgname=phoc
pkgdesc="A compositor for phones based on wlroots"
pkgver=0.21.1.r0.g0bcb879
pkgrel=1
_arches=all
arch=(
    x86_64
    aarch64
)
license=(GPL)
url=https://gitlab.gnome.org/World/Phosh/phoc
depends=(
    gobject-introspection
    gnome-desktop
    libinput
    mutter
    wayland-protocols
    seatd
    xcb-util-wm
    xcb-util-renderutil
)
makedepends=(
    meson
    ninja
    vulkan-headers
    glslang
)

_commit=0bcb87984fc08f7417baf9aa25a1707126620a6e
_wlroots_commit=9b77a484d9581fb6ce4b0afcd44dd728709d8ba7
source=(
    "git+$url#commit=$_commit"
    "git+https://source.puri.sm/Librem5/wlroots#commit=$_wlroots_commit"
)
sha256sums=(
    SKIP
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$pkgname"
    git submodule init
    git submodule set-url subprojects/wlroots "${srcdir}/wlroots"
    git submodule update

    cd "${srcdir}/wlroots"

    while IFS= read -r patch; do echo "APPLYING $patch"; patch -p1 -i "debian/patches/$patch"; done < debian/patches/series
}

build() {
    arch-meson ${pkgname} output -Dtests=false \
        -Dembed-wlroots=enabled --default-library=static \
        -Dwlroots:examples=false \
        -Dwlroots:xcb-errors=disabled
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    # Install scale-to-fit helper
    install -Dm755 ${pkgname}/helpers/scale-to-fit "$pkgdir"/usr/bin/scale-to-fit

    # Remove unnecessary files
    rm -r "$pkgdir"/usr/lib
    rm -r "$pkgdir"/usr/include
}
