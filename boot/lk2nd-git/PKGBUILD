_mode=cross
pkgbase=boot-lk2nd-git
pkgname=(
    boot-lk2nd-msm8916-git
    boot-lk2nd-msm8974-git
    boot-lk2nd-msm8226-git
)
pkgver=r5741.1db86570
pkgrel=1
_arches=specific
arch=(aarch64)
license=(MIT)
url=https://github.com/msm8916-mainline/lk2nd
provides=(boot-lk2nd)
makedepends=(
    python
    dtc
    arm-none-eabi-gcc
)
_commit=1db86570a8c355c8071244223d7f7d0407cf2f40
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd "$srcdir"/lk2nd
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir"/lk2nd
    rm -rf build-lk2nd-msm8916 build-lk2nd-msm8974 build-lk2nd-msm8226
    make TOOLCHAIN_PREFIX=arm-none-eabi- lk2nd-msm8916
    make TOOLCHAIN_PREFIX=arm-none-eabi- lk2nd-msm8974
    make TOOLCHAIN_PREFIX=arm-none-eabi- lk2nd-msm8226
}

package_boot-lk2nd-msm8916-git() {
    provides+=(lk2nd-msm8916)
    install -Dm644 "$srcdir"/lk2nd/build-lk2nd-msm8916/lk2nd.img "${pkgdir}/boot/lk2nd.img"
    install -Dm644 "$srcdir"/lk2nd/build-lk2nd-msm8916/lk2nd-appended-dtb.img "${pkgdir}/boot/lk2nd-appended-dtb.img"
}

package_boot-lk2nd-msm8974-git() {
    provides+=(lk2nd-msm8974)
    install -Dm644 "$srcdir"/lk2nd/build-lk2nd-msm8974/lk2nd.img "${pkgdir}/boot/lk2nd.img"
    #install -Dm644 "$srcdir"/lk2nd/build-lk2nd-msm8974/lk2nd-appended-dtb.img "${pkgdir}/boot/lk2nd-appended-dtb.img"
}

package_boot-lk2nd-msm8226-git() {
    provides+=(lk2nd-msm8226)
    #install -Dm644 "$srcdir"/lk2nd/build-lk2nd-msm8226/lk2nd.img "${pkgdir}/boot/lk2nd.img"
    install -Dm644 "$srcdir"/lk2nd/build-lk2nd-msm8226/lk2nd-appended-dtb.img "${pkgdir}/boot/lk2nd-appended-dtb.img"
}
