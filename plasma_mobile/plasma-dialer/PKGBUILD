# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Furkan Kardame <tech@fkardame.com>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
pkgname=plasma-dialer
pkgdesc="Phone Dialer for Plasma Mobile"
pkgver=23.01.0.r0.g0eb7343
pkgrel=2
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL-3.0-or-later)
depends=(
    desktop-file-utils
    kcontacts5
    modemmanager-qt5
    kpeople5
    telepathy-qt
    libphonenumber
    kdbusaddons5
    kirigami-addons5
    knotifications5
    telepathy-mission-control
    pulseaudio-qt
    kio5
    qt5-declarative
    qt5-feedback
    qt5-quickcontrols2
    qtmpris
    callaudiod
    plasma-wayland-protocols
)
makedepends=(
    cmake
    extra-cmake-modules
    kdoctools5
)
_commit=0eb7343125c472dd842d2b049332bc5086624acf
source=(
    "git+https://invent.kde.org/plasma-mobile/plasma-dialer.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../$pkgname \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_INSTALL_DIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DCMAKE_BUILD_TYPE=Release
    make
}

check() {
    cd build
    ctest --output-on-failure
}

package() {
    cd build
    make DESTDIR="$pkgdir/" install
}
