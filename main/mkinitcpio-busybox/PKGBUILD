_mode=cross
pkgname=mkinitcpio-busybox-kupfer
pkgdesc="Base initramfs tools"
pkgver=1.35.0
pkgrel=1
_arches=all
arch=(
    aarch64
    x86_64
)
license=(GPL)
url=https://www.busybox.net
provides=(mkinitcpio-busybox)
conflicts=(mkinitcpio-busybox)
depends=(
    glibc
    libxcrypt
)
options=(!buildflags)
source=(
    "https://busybox.net/downloads/busybox-$pkgver.tar.bz2"
    config
)
sha256sums=(
    faeeb244c35a348a334f4a59e44626ee870fb07b6884d68c10ae8bc19f83a694
    1e6c9c0c31a01b84cbafc7e46505ef915233e4fef86e6695e3957c655dfa73c5
)

prepare() {
    cd "busybox-$pkgver"

    local safeflags="-march=armv8-a -Os -pipe -fno-strict-aliasing"

    sed 's|^\(CONFIG_EXTRA_CFLAGS\)=.*|\1="'"$safeflags"'"|' "$srcdir/config" >.config
}

build() {
    # reproducible build
    export KCONFIG_NOTIMESTAMP=1
    LDFLAGS="$LDFLAGS -static" make -C "busybox-$pkgver"
}

package() {
    install -Dm755 "$srcdir/busybox-$pkgver/busybox" "$pkgdir/usr/lib/initcpio/busybox"
}
